buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:3.378.0'
//        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.1-SNAPSHOT'
    }
}

apply plugin: 'android'
apply plugin: 'newrelic'
//apply plugin: 'android-test'

group 'org.ccci.gto.android'
version '0.9.4-SNAPSHOT'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 10
        targetSdkVersion 19

        buildConfigField "String", "ARCLIGHT_API_KEY", "\"52d40edd323002.56665774\""
        buildConfigField "String", "ARCLIGHT_API_URI", "\"https://api.arclight.org/\""
        buildConfigField "String", "EKKO_SMX_API_URI", "\"https://services.gcx.org/ekko/v1/\""
        buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA42125d6f913bc0928dd33727113953b59e070b5d\""
        buildConfigField "long", "THEKEY_CLIENTID", "85613451684391165L"
        buildConfigField "String", "YOUTUBE_API_KEY", "\"AIzaSyAuKlaTyF_c0DSNP8MyPe6ABoFq_5cDFh8\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA10ca156dd8c21018710ba9ba8fc4bf7e019aa80a\""
            buildConfigField "String", "YOUTUBE_API_KEY", "\"AIzaSyDeJCVnQsv2J7SbjGdJa2jcdfz2cSw-9B0\""
        }
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        development {
            buildConfigField "String", "EKKO_SMX_API_URI", "\"https://servicesdev.gcx.org/ekko/v1/\""
        }
        production {
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
        credentials {
            username gcxMavenRepositoryUsername
            password gcxMavenRepositoryPassword
        }
    }
    // This is a temporary workaround until ViewPagerIndicator is distributed as an aar
    maven {
        url 'https://github.com/Dahlgren/vpi-aar/raw/master/'
    }
}

dependencies {
    compile group: 'com.android.support',    name: 'appcompat-v7',               version: '19.1.0',         ext: 'aar'
    compile group: 'com.android.support',    name: 'support-v4',                 version: '19.1.0',         ext: 'jar'
    compile group: 'com.google.android',     name: 'youtube-android-player-api', version: '1.0.0',          ext: 'jar'
    compile group: 'com.google.guava',       name: 'guava',                      version: '17.0',           ext: 'jar'
    compile group: 'org.slf4j',              name: 'slf4j-api',                  version: '1.7.7',          ext: 'jar'
    compile group: 'org.slf4j',              name: 'slf4j-android',              version: '1.7.7',          ext: 'jar'
    compile group: 'com.viewpagerindicator', name: 'library',                    version: '2.4.2-SNAPSHOT', ext: 'aar'
    compile group: 'com.thinkfree.android',  name: 'showlicense',                version: '0.1',            ext: 'jar'
    compile group: 'org.ccci.gto.android',   name: 'gto-support',                version: '0.3.0',          ext: 'jar'
    compile group: 'me.thekey.android',      name: 'thekey-api',                 version: '0.6.0',          ext: 'jar'
    compile group: 'me.thekey.android',      name: 'thekey-lib',                 version: '0.6.0',          ext: 'aar'

    compile 'com.newrelic.agent.android:android-agent:3.378.0'

//    testCompile 'junit:junit:4.11'
//    testCompile 'org.robolectric:robolectric:2.3'
//    testCompile 'com.squareup:fest-android:1.0.+'
}

apply from: 'maven_publish.gradle'
afterEvaluate { project ->
    artifacts {
        archives(file("build/outputs/apk/ekko-production-release.apk")) {
            type 'apk'
            builtBy assembleProductionRelease
        }
        archives(file("build/outputs/proguard/production/release/mapping.txt")) {
            classifier 'mapping'
            type 'txt'
            builtBy proguardProductionRelease
        }
//        archives androidSourcesJar
//        archives androidJavadocsJar
    }
}
