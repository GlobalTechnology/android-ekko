buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

apply plugin: 'android'

group 'org.ccci.gto.android'
version '0.9.2-SNAPSHOT'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    defaultConfig {
        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 8
        targetSdkVersion 18
    }

    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
        credentials {
            username gcxMavenRepositoryUsername
            password gcxMavenRepositoryPassword
        }
    }
    // This is a temporary workaround until ViewPagerIndicator is distributed as an aar
    maven {
        url 'https://github.com/Dahlgren/vpi-aar/raw/master/'
    }
}

dependencies {
    compile group: 'com.android.support', name: 'support-v4', version: '18.0.0', ext: 'jar'
    compile group: 'org.slf4j', name: 'slf4j-android', version: '1.6.1-RC1', ext: 'jar'
    compile group: 'com.actionbarsherlock', name: 'actionbarsherlock', version: '4.4.0', ext: 'aar'
    compile group: 'com.viewpagerindicator', name: 'library', version: '2.4.2-SNAPSHOT', ext: 'aar'
    compile group: 'org.ccci.gto.android', name: 'gto-support', version: '0.1.0-SNAPSHOT', ext: 'jar'
    compile group: 'me.thekey.android', name: 'thekey-api', version: '0.4.0-SNAPSHOT', ext: 'jar'
    compile group: 'me.thekey.android', name: 'thekey-lib', version: '0.4.0-SNAPSHOT', ext: 'aar'
}
