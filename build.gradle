buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:3.429.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'newrelic'

group 'org.ccci.gto.android'
version '0.9.6-SNAPSHOT'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 20
    buildToolsVersion "20"

    defaultConfig {
        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 10
        targetSdkVersion 20

        buildConfigField "String", "ARCLIGHT_API_KEY", "\"52d40edd323002.56665774\""
        buildConfigField "String", "ARCLIGHT_API_URI", "\"https://api.arclight.org/\""
        buildConfigField "String", "EKKO_SMX_API_URI", "\"https://services.gcx.org/ekko/v1/\""
        buildConfigField "String", "GOOGLE_ANALYTICS_CLIENT_ID", "\"UA-29919940-4\""
        buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA42125d6f913bc0928dd33727113953b59e070b5d\""
        buildConfigField "long", "THEKEY_CLIENTID", "85613451684391165L"
        buildConfigField "String", "YOUTUBE_API_KEY", "\"AIzaSyAuKlaTyF_c0DSNP8MyPe6ABoFq_5cDFh8\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "String", "EKKO_SMX_API_URI", "\"https://servicesdev.gcx.org/ekko/v1/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA10ca156dd8c21018710ba9ba8fc4bf7e019aa80a\""
            buildConfigField "String", "YOUTUBE_API_KEY", "\"AIzaSyDeJCVnQsv2J7SbjGdJa2jcdfz2cSw-9B0\""
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), file('proguard.pro')
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
        credentials {
            username gcxMavenRepositoryUsername
            password gcxMavenRepositoryPassword
        }
    }
    // This is a temporary workaround until ViewPagerIndicator is distributed as an aar
    maven {
        url 'https://github.com/Dahlgren/vpi-aar/raw/master/'
    }
}

configurations {
    // exclude SLF4J because JesusFilm eventtracker bundles the dependencies in it's own jar
    compile.exclude group: 'org.slf4j'
}

dependencies {
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.google.android:youtube-android-player-api:1.0.0'
    compile 'com.google.android.gms:play-services:5.2.08'
    compile 'com.google.guava:guava:18.0'
    compile 'com.jesusfilmmedia:eventtracker:2.0'
    compile 'com.newrelic.agent.android:android-agent:3.429.0'
    compile 'com.viewpagerindicator:library:2.4.2-SNAPSHOT@aar'
    compile 'com.thinkfree.android:showlicense:0.1'
    compile 'me.thekey.android:thekey-lib:0.7.0-SNAPSHOT'
    compile 'org.ccci.gto.android:gto-support:0.4.0-SNAPSHOT'
//    compile 'org.slf4j:slf4j-android:1.7.7'
}

apply from: 'maven_publish.gradle'
afterEvaluate { project ->
    artifacts {
        archives(file("build/outputs/proguard/release/mapping.txt")) {
            classifier 'mapping'
            type 'txt'
            builtBy proguardRelease
        }
        archives artifactSourcesJar
//        archives artifactJavadocJar
    }
}
