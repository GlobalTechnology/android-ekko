buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.3'
        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.1-SNAPSHOT'
    }
}

apply plugin: 'android'
apply plugin: 'android-test'

group 'org.ccci.gto.android'
version '0.9.3'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 8
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        development {
        }
        production {
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
        credentials {
            username gcxMavenRepositoryUsername
            password gcxMavenRepositoryPassword
        }
    }
    // This is a temporary workaround until ViewPagerIndicator is distributed as an aar
    maven {
        url 'https://github.com/Dahlgren/vpi-aar/raw/master/'
    }
}

dependencies {
    compile group: 'com.android.support',    name: 'appcompat-v7',               version: '19.0.1',         ext: 'aar'
    compile group: 'com.android.support',    name: 'support-v4',                 version: '19.0.1',         ext: 'jar'
    compile group: 'com.google.android',     name: 'youtube-android-player-api', version: '1.0.0',          ext: 'jar'
    compile group: 'org.slf4j',              name: 'slf4j-android',              version: '1.6.1-RC1',      ext: 'jar'
    compile group: 'com.viewpagerindicator', name: 'library',                    version: '2.4.2-SNAPSHOT', ext: 'aar'
    compile group: 'com.thinkfree.android',  name: 'showlicense',                version: '0.1',            ext: 'jar'
    compile group: 'org.ccci.gto.android',   name: 'gto-support',                version: '0.2.1',          ext: 'jar'
    compile group: 'me.thekey.android',      name: 'thekey-api',                 version: '0.5.1',          ext: 'jar'
    compile group: 'me.thekey.android',      name: 'thekey-lib',                 version: '0.5.1',          ext: 'aar'

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.1.+'
    testCompile 'com.squareup:fest-android:1.0.+'
}

apply from: 'maven_publish.gradle'
afterEvaluate { project ->
    artifacts {
        archives(file("build/apk/ekko-production-release.apk")) {
            type 'apk'
            builtBy assembleProductionRelease
        }
        archives(file("build/proguard/production/release/mapping.txt")) {
            classifier 'mapping'
            type 'txt'
            builtBy proguardProductionRelease
        }
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
